plugins {
	id 'java-library'
	id 'maven-publish'

	id 'net.minecraftforge.gradle' version '6.0.14'
	id 'org.parchmentmc.librarian.forgegradle' version '1.2.0'

	id 'org.spongepowered.mixin' version '0.7.38'
}

group = mod_group_id
version = mod_version

base {
	archivesName = mod_id
}

mixin {
	add sourceSets.main, "mixins.hbm_m.refmap.json"
	config "mixins.hbm_m.json"

	if(project.hasProperty("mixinDebug")) {
		debug.verbose = true
		debug.export = true
	}
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
	mappings channel: mapping_channel, version: mapping_version

	// This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
	// It is REQUIRED to be set to true for this template to function.
	copyIdeResources = true

	runs {
		configureEach {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES' // 'SCAN' / 'REGISTRIES' / 'REGISTRYDUMP'
			property 'forge.logging.console.level', project.findProperty("logLevel") ?: "info"

			mods {
				"${mod_id}" {
					source sourceSets.main
				}
			}
		}

		client {
			property 'forge.enabledGameTestNamespaces', mod_id
			jvmArgs = [ "-Xmx4G", "-XX:+UseG1GC", "-XX:MaxGCPauseMillis=50"]
		}

		server {
			property 'forge.enabledGameTestNamespaces', mod_id
			args '--nogui'
			jvmArgs = [ "-Xmx3G", "-XX:+UseG1GC", "-XX:MaxGCPauseMillis=100"]
		}

		gameTestServer { // Quickly and short server-start from test-passes & quick-stacktrace-test
			property 'forge.enabledGameTestNamespaces', mod_id
			jvmArgs = [ "-Xmx2G", "-XX:+UseG1GC"]
		}

		data { // Data generation settings
			workingDirectory project.file('run-data')
			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories { // Put repositories for dependencies here
	mavenCentral()
	if (project.hasProperty("useMavenLocal")) {
		mavenLocal()
	}
	
	maven { url = 'https://maven.minecraftforge.net/' }
	maven { url = "https://maven.parchmentmc.org/" }
	maven { url = "https://maven.shedaniel.me/" }
	
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = "CurseForge"
		url = "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	
	api(fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136"))

	runtimeOnly fg.deobf("maven.modrinth:spark:1.10.53-forge")
	runtimeOnly fg.deobf("curse.maven:jei-238222:7270446")
	
	compileOnly("curse.maven:oculus-581495:6020952") {
	    transitive = false
	}
	
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.named('processResources', ProcessResources).configure {
	var replaceProperties = [
			minecraft_version: minecraft_version, 
			minecraft_version_range: minecraft_version_range,
			forge_version: forge_version, 
			forge_version_range: forge_version_range,
			loader_version_range: loader_version_range,
			mod_id: mod_id, 
			mod_name: mod_name, 
			mod_license: mod_license,
			mod_version: project.version,
			mod_authors: mod_authors, 
			mod_description: mod_description,
	]
	inputs.properties replaceProperties

	filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
		expand replaceProperties + [project: project]
	}
}

tasks.named('jar', Jar).configure {
	manifest {
		attributes([
				'Specification-Title'     : mod_id,
				'Specification-Vendor'    : mod_authors,
				'Specification-Version'   : '1',
				'Implementation-Title'    : project.name,
				'Implementation-Version'  : project.version,
				'Implementation-Vendor'   : mod_authors,
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
		])
	}

	finalizedBy 'reobfJar'
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

publishing {
	publications {
		register('mavenJava', MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			url "file://${project.projectDir}/mcmodsrepo"
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE // INCLUDE / EXCLUDE / WARN
}

tasks.withType(JavaExec).configureEach { // Enable writing in server console
	if (it.name == 'runServer') {
		standardInput = System.in
	}
}